namespace team

import common

#
# Structures for linked_apps/list_member_linked_apps
#

struct ListMemberAppsArg
    team_member_id String
        "The team member id."

    example default
        team_member_id = "dbmid:AAFdgehTzw7WlXhZJsbGCLePe8RvQGYDr-I"

struct ApiApp
    "Information on linked third party applications."

    app_id String
        "The application unique id."
    app_name String
        "The application name."
    publisher String?
        "The application publisher name."
    publisher_url String?
        "The publisher's URL."
    linked common.DropboxTimestamp?
        "The time this application was linked."
    is_app_folder Boolean
        "Whether the linked application uses a dedicated folder."

    example default
        app_id = "dbaid:AAFdgehTzw7WlXhZJsbGCLePe8RvQGYDr-I"
        app_name = "Notes"
        publisher = "Notes company"
        publisher_url = "http://company.com"
        linked = "2015-05-12T15:50:38Z"
        is_app_folder = true

struct ListMemberAppsResult
    linked_api_apps List(ApiApp)
        "List of third party applications linked by this team member."

union ListMemberAppsError
    "Error returned by :route:`linked_apps/list_member_linked_apps`."

    member_not_found
        "Member not found."

#
# Route: linked_apps/list_member_linked_apps
#

route linked_apps/list_member_linked_apps(ListMemberAppsArg, ListMemberAppsResult, ListMemberAppsError)
    "List all linked applications of the team member.

    Note, this endpoint does not list any team-linked applications."

    attrs
        auth = "team"
        scope = "sessions.list"

#
# Structs for linked_apps/list_members_linked_apps
#

struct ListMembersAppsArg
    "Arguments for :route:`linked_apps/list_members_linked_apps`."

    cursor String?
        "At the first call to the :route:`linked_apps/list_members_linked_apps` the cursor shouldn't be
        passed. Then, if the result of the call includes a cursor, the following requests should
        include the received cursors in order to receive the next sub list of the team applications."

struct MemberLinkedApps
    "Information on linked applications of a team member."

    team_member_id String
        "The member unique Id."
    linked_api_apps List(ApiApp)
        "List of third party applications linked by this team member."

struct ListMembersAppsResult
    "Information returned by :route:`linked_apps/list_members_linked_apps`."

    apps List(MemberLinkedApps)
        "The linked applications of each member of the team."
    has_more Boolean
        "If true, then there are more apps available. Pass the
        cursor to :route:`linked_apps/list_members_linked_apps` to retrieve the rest."
    cursor String?
        "Pass the cursor into :route:`linked_apps/list_members_linked_apps` to receive the next
        sub list of team's applications."

union ListMembersAppsError
    "Error returned by :route:`linked_apps/list_members_linked_apps`."

    reset
        "Indicates that the cursor has been invalidated. Call
        :route:`linked_apps/list_members_linked_apps` again with an empty cursor to obtain a new cursor."

#
# Route: linked_apps/list_members_linked_apps
#
route linked_apps/list_members_linked_apps(ListMembersAppsArg, ListMembersAppsResult, ListMembersAppsError)
    "List all applications linked to the team members' accounts.

    Note, this endpoint does not list any team-linked applications."

    attrs
        auth = "team"
        scope = "sessions.list"

#
# Structs for linked_apps/revoke_linked_app
#

struct RevokeLinkedApiAppArg
    app_id String
        "The application's unique id."
    team_member_id String
        "The unique id of the member owning the device."
    keep_app_folder Boolean = true
        "This flag is not longer supported, the application dedicated folder (in case the application uses
         one) will be kept."

union RevokeLinkedAppError
    "Error returned by :route:`linked_apps/revoke_linked_app`."

    app_not_found
        "Application not found."
    member_not_found
        "Member not found."
    app_folder_removal_not_supported
        "App folder removal is not supported."


#
# Route: linked_apps/revoke_linked_app
#

route linked_apps/revoke_linked_app(RevokeLinkedApiAppArg, Void, RevokeLinkedAppError)
    "Revoke a linked application of the team member."

    attrs
        auth = "team"
        scope = "sessions.modify"

#
# structs for linked_apps/revoke_linked_app_batch
#

struct RevokeLinkedApiAppBatchArg
    revoke_linked_app List(RevokeLinkedApiAppArg)

struct RevokeLinkedAppStatus

    success Boolean
        "Result of the revoking request."
    error_type RevokeLinkedAppError?
        "The error cause in case of a failure."

struct RevokeLinkedAppBatchResult
    revoke_linked_app_status List(RevokeLinkedAppStatus)

union RevokeLinkedAppBatchError
    "Error returned by :route:`linked_apps/revoke_linked_app_batch`."

#
# Route: linked_apps/revoke_linked_app_batch
#

route linked_apps/revoke_linked_app_batch(RevokeLinkedApiAppBatchArg, RevokeLinkedAppBatchResult, RevokeLinkedAppBatchError)
    "Revoke a list of linked applications of the team members."

    attrs
        auth = "team"
        scope = "sessions.modify"

#
# Deprecated endpoints
#

struct ListTeamAppsArg
    "Arguments for :route:`linked_apps/list_team_linked_apps`."

    cursor String?
        "At the first call to the :route:`linked_apps/list_team_linked_apps` the cursor shouldn't be
        passed. Then, if the result of the call includes a cursor, the following requests should
        include the received cursors in order to receive the next sub list of the team applications."

struct ListTeamAppsResult
    "Information returned by :route:`linked_apps/list_team_linked_apps`."

    apps List(MemberLinkedApps)
        "The linked applications of each member of the team."
    has_more Boolean
        "If true, then there are more apps available. Pass the
        cursor to :route:`linked_apps/list_team_linked_apps` to retrieve the rest."
    cursor String?
        "Pass the cursor into :route:`linked_apps/list_team_linked_apps` to receive the next
        sub list of team's applications."

union ListTeamAppsError
    "Error returned by :route:`linked_apps/list_team_linked_apps`."

    reset
        "Indicates that the cursor has been invalidated. Call
        :route:`linked_apps/list_team_linked_apps` again with an empty cursor to obtain a new cursor."

route linked_apps/list_team_linked_apps(ListTeamAppsArg, ListTeamAppsResult, ListTeamAppsError) deprecated by linked_apps/list_members_linked_apps
    "List all applications linked to the team members' accounts.

    Note, this endpoint doesn't list any team-linked applications."

    attrs
        auth = "team"
